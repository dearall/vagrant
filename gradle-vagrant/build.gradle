buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/central' }
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy:3.0.9'
    }
}

apply from: 'vagrant.gradle'

// web war 文件的 group, name 属性读取，命令行使用 -Pwar= 获取要部署的 web war 文件 GA 属性
def propGA = project.hasProperty('war') ? project.getProperty('war') : 'manning'
logger.quiet "Loading configuration for warGA '$propGA'."

def webWarFile = file("$rootDir/config/webWarArtifact.groovy")
def parsedWarGA = new ConfigSlurper(propGA).parse(webWarFile.toURI().toURL())

// 环境部署环境属性读取，命令行使用 -Penv= 设置环境值
def env = project.hasProperty('env') ? project.getProperty('env') : 'test'
logger.quiet "Loading configuration for environment '$env'."

def configFile = file("$rootDir/config/buildConfig.groovy")
def parsedConfig = new ConfigSlurper(env).parse(configFile.toURI().toURL())

ext {
    warGA = parsedWarGA
    config = parsedConfig
}

apply from: 'downloadWar.gradle'

tasks.register('deployWar') {
    doLast {
        logger.quiet "Deploying WAR file to $config.server.hostname via SSH at port $config.server.sshPort."
        logger.quiet "download url: ${config.binaryRepository.releaseUrl}"

        logger.quiet "warGA.group: ${warGA.group}"
        logger.quiet "warGA.name: ${warGA.name}"
    }
}



