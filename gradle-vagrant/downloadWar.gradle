repositories {
    maven {
        url config.binaryRepository.releaseUrl
        allowInsecureProtocol = true
    }
}

ext {
    //定义要下载和部署的 war 文件版本号
    //war_version = '1.0.1'
    war_version = 'latest.release'

    // war 文件版本号说明：
    // latest.release ： 虽然适合于下载，但不适合于向远程机器传送 task,
    // task 必须在配置阶段明确版本号，而 'latest.release' 是在 fetchWebWar task 执行阶段确定的最终版本号，在配置阶段具体
    // 版本是未知的。因此这个具体的版本号无法在 ScpTask 的配置阶段得到具体的值，因此不能使用 'latest.release' 作为版本号
    //
    // 注： 这只是在开发测试时的方案，在 CI 环境下，不受此控制。CI 环境具体的版本号，由构建管道上级构建步骤中携带的代码版本号环境变量
    // 确定，因此这个版本号也就不起作用了。CI 环境变量所确定的版本号是最真实可靠，真实反映出当前构建的确定版本。
    war_artifact_version = war_version
    if(System.env.VERSION_MAJOR) {
        war_artifact_version = "${System.env.VERSION_MAJOR}.${System.env.VERSION_MINOR}"

        if (System.env.VERSION_FIX) {
            war_artifact_version = "${System.env.VERSION_MAJOR}.${System.env.VERSION_MINOR}.${System.env.VERSION_FIX}"
        }
    }
    war_artifact_group = System.env.WEB_APP_WAR_GROUP ?: "${warGA.group}"
    war_artifact_name  = System.env.WEB_APP_WAR_NAME ?: "${warGA.name}"

    downloadDir = file("$buildDir/download/artifacts")
}

configurations {
    warFileFetching {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        }
    }
}

dependencies {
    warFileFetching (group: war_artifact_group, name: war_artifact_name, version: war_artifact_version, ext: 'war')
}

tasks.register('fetchWebWar', Copy) {
    group 'Fetch File'
    description 'fetch war file from maven repository'

    from configurations.warFileFetching
    into downloadDir
    /*
    用于测试
    rename { String filename ->
        logger.quiet 'fetchWebWar filename: ' + filename
        download_filename = filename
        return null
    }*/

    /*
    没有获得预期效果
    outputs.doNotCacheIf 'do not cache this download', {
        return true
    }
    */
    doFirst {
        logger.quiet 'war_artifact_group: ' + war_artifact_group
        logger.quiet 'war_artifact_name: ' + war_artifact_name
        logger.quiet 'war_artifact_version: ' +  war_artifact_version
    }
}





